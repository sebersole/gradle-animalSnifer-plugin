plugins {
	id 'java-gradle-plugin'
	id 'java'
	id 'groovy'
	id 'idea'
	id 'eclipse'
	id "com.gradle.plugin-publish" version "0.9.10"
}

group = 'org.hibernate.build.animalsniffer'
version = '2.0.0.Alpha1'

repositories {
	mavenCentral()
}

dependencies {
	compile gradleApi()
	compile 'org.codehaus.mojo:animal-sniffer:1.16'

	testCompile 'junit:junit:4.12'
}

gradlePlugin {
	plugins {
		checkPlugin {
			id = 'org.hibernate.build.animal-sniffer-check'
			implementationClass = 'org.hibernate.build.animalsniffer.CheckPlugin'
		}
		generatePlugin {
			id = 'org.hibernate.build.animal-sniffer-generate'
			implementationClass = 'org.hibernate.build.animalsniffer.GeneratePlugin'
		}
	}
}

pluginBundle {
	website = 'http://github.com/sebersole/gradle-animalSniffer-plugin'
	vcsUrl = 'https://github.com/sebersole/gradle-animalSniffer-plugin'

	tags = ['animalsniffer', 'animal-sniffer', 'ABI']

	plugins {
		checkPlugin {
			id = 'org.hibernate.build.animal-sniffer-check'
			displayName = 'Support for checking project API usage as defined by Animal Sniffer signature files'
			description = 'Support for checking project API usage as defined by Animal Sniffer signature files'
		}
		generatePlugin {
			id = 'org.hibernate.build.animal-sniffer-generate'
			displayName = 'Support for generating a Animal Sniffer signature file for the project API'
			description = 'Support for generating a Animal Sniffer signature file for the project API'
		}
	}
}

idea {
    project {
        jdkName = '1.6'
        languageLevel = '1.6'

        vcs = 'Git'
    }
    module {
            downloadSources = true
    }
}

task copyResourcesToIntelliJOutFolder(dependsOn: tasks.processTestResources) {
	doLast {
		copy {
			from "$buildDir/resources/test"
			into 'out/test/resources'
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

