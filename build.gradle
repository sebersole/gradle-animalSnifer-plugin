repositories {
	mavenCentral()
}

buildscript {
	repositories {
		mavenCentral()

		maven {
			name 'jboss-nexus'
			url "http://repository.jboss.org/nexus/content/groups/public/"
		}
		maven {
			name "jboss-snapshots"
			url "http://snapshots.jboss.org/maven2/"
		}
	}
	dependencies {
		classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
	}
}

apply plugin: "groovy"
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

dependencies {
	compile gradleApi()
	compile localGroovy()

	// AnimalSniffer suffers from a bug in 1.14 which hits us hard.
	// But unfortunately every version prior to 1.14 uses a version
	// of ASM that does not support Java 8.  So we need to use
	// AnimalSniffer 1.13 (bug not present) but override its ASM
	// version to one that supports Java 8
	compile 'org.codehaus.mojo:animal-sniffer:1.13'
	compile 'org.ow2.asm:asm-all:5.0.3'
}


idea {
    project {
        jdkName = '1.6'
        languageLevel = '1.6'

        vcs = 'Git'
    }
    module {
            downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

group = 'org.hibernate.build.gradle'
version = '2.0.0-SNAPSHOT'

publishing {
	publications {
		plugin(MavenPublication) {
			from components.java

			artifact sourcesJar {
				classifier "sources"
			}

			pom.withXml {
				// append additional metadata
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST

					name 'Gradle plugin for AnimalSniffer integration'
					description 'Integrates AnimalSniffer functionality into a Gradle build'

					url 'https://github.com/sebersole/gradle-animalSniffer-plugin'
//				organization {
//					name 'Hibernate.org'
//					url 'http://hibernate.org'
//				}

					issueManagement {
						system 'github'
						url 'https://github.com/sebersole/gradle-animalSniffer-plugin/issues'
					}

					scm {
						url 'https://github.com/sebersole/gradle-animalSniffer-plugin'
						connection 'scm:git:https://github.com/sebersole/gradle-animalSniffer-plugin.git'
						developerConnection 'scm:git:git@github.com:sebersole/gradle-animalSniffer-plugin.git'
					}

					licenses {
						license {
							name 'Apache License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'sebersole'
							name 'Steve Ebersole'
							organization 'Hibernate.org'
							organizationUrl 'http://hibernate.org'
						}
					}
				}

				// TEMPORARY : currently Gradle Publishing feature is exporting dependencies as 'runtime' scope,
				//      rather than 'compile'; fix that.
				asNode().dependencies[0].dependency.each {
					it.scope[0].value = 'compile'
				}
			}
		}
	}

	repositories {
		maven {
			if ( version.endsWith( 'SNAPSHOT' ) ) {
				name 'jboss-snapshots-repository'
				url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
			}
			else {
				name 'jboss-releases-repository'
				url 'https://repository.jboss.org/nexus/service/local/staging/deploy/maven2/'
			}
		}
	}
}

task sourcesJar(type: Jar, dependsOn: compileJava) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

